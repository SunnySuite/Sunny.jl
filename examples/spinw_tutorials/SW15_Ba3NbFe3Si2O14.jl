# # SW15 - Baâ‚ƒNbFeâ‚ƒSiâ‚‚Oâ‚â‚„
#
# This is a Sunny port of [SpinW Tutorial
# 15](https://spinw.org/tutorials/15tutorial), originally authored by Sandor
# Toth. It calculates the linear spin wave theory spectrum of Baâ‚ƒNbFeâ‚ƒSiâ‚‚Oâ‚â‚„.
# The ground state is an incommensurate spiral, which can be directly studied
# using the functions [`minimize_spiral_energy!`](@ref) and
# [`SpinWaveTheorySpiral`](@ref).

# Load packages 

using Sunny, GLMakie

# Specify the Baâ‚ƒNbFeâ‚ƒSiâ‚‚Oâ‚â‚„ [`Crystal`](@ref) cell following [Marty et al.,
# Phys. Rev. Lett. **101**, 247201
# (2008)](http://dx.doi.org/10.1103/PhysRevLett.101.247201).

units = Units(:meV, :angstrom)
a = b = 8.539 # (Ã…)
c = 5.2414
latvecs = lattice_vectors(a, b, c, 90, 90, 120)
types = ["Fe", "Nb", "Ba", "Si", "O", "O", "O"]
positions = [[0.24964,0,0.5], [0,0,0], [0.56598,0,0], [2/3,1/3,0.5220],
             [2/3,1/3,0.2162], [0.5259,0.7024,0.3536], [0.7840,0.9002,0.7760]]
langasite = Crystal(latvecs, positions, 150; types)
cryst = subcrystal(langasite, "Fe")
view_crystal(cryst)

# Create a [`System`](@ref) and set exchange interactions as parametrized in
# [Loire et al., Phys. Rev. Lett. **106**, 207201
# (2011)](http://dx.doi.org/10.1103/PhysRevLett.106.207201).

sys = System(cryst, [1 => Moment(s=5/2, g=2)], :dipole)
Jâ‚ = 0.85
Jâ‚‚ = 0.24
Jâ‚ƒ = 0.053
Jâ‚„ = 0.017
Jâ‚… = 0.24
set_exchange!(sys, Jâ‚, Bond(3, 2, [1,1,0]))
set_exchange!(sys, Jâ‚„, Bond(1, 1, [0,0,1]))
set_exchange!(sys, Jâ‚‚, Bond(1, 3, [0,0,0]))

# The final two exchanges are set according to the desired chirality ``Ïµ_T`` of
# the magnetic structure.

ÏµT = -1
if ÏµT == -1
    set_exchange!(sys, Jâ‚ƒ, Bond(2, 3, [-1,-1,1]))
    set_exchange!(sys, Jâ‚…, Bond(3, 2, [1,1,1]))
elseif ÏµT == 1
    set_exchange!(sys, Jâ‚…, Bond(2, 3, [-1,-1,1]))
    set_exchange!(sys, Jâ‚ƒ, Bond(3, 2, [1,1,1]))
else
    error("Chirality must be Â±1")
end

# This compound is known to have a spiral order with approximate propagation
# wavevector ``ğ¤ â‰ˆ [0, 0, 1/7]``. Search for this magnetic order with
# [`minimize_spiral_energy!`](@ref). Due to reflection symmetry, one of two
# possible propagation wavevectors may appear, ``ğ¤ = Â± [0, 0, 0.1426â€¦]``.
# Note that ``k_z = 0.1426â€¦`` is very close to ``1/7 = 0.1428â€¦``.

axis = [0, 0, 1]
randomize_spins!(sys)
k = minimize_spiral_energy!(sys, axis)

# We can visualize the full magnetic cell using [`repeat_periodically_as_spiral`](@ref),
# which includes 7 rotated copies of the chemical cell.

sys_enlarged = repeat_periodically_as_spiral(sys, (1, 1, 7); k, axis)
plot_spins(sys_enlarged; color=[S[1] for S in sys_enlarged.dipoles])

# One could perform a spin wave calculation using either
# [`SpinWaveTheory`](@ref) on `sys_enlarged`, or [`SpinWaveTheorySpiral`](@ref)
# on the original `sys`. The latter has some restrictions on the interactions,
# but allows for our slightly incommensurate wavevector ``ğ¤``.

measure = ssf_perp(sys)
swt = SpinWaveTheorySpiral(sys; measure, k, axis)

# Calculate broadened intensities for a path ``[0, 1, L]`` through reciprocal
# space

qs = [[0, 1, -1], [0, 1, -1+1], [0, 1, -1+2], [0, 1, -1+3]]
path = q_space_path(cryst, qs, 400)
energies = range(0, 6, 400)
res = intensities(swt, path; energies, kernel=gaussian(fwhm=0.25))
plot_intensities(res; units, saturation=0.7, colormap=:jet, title="Scattering intensities")

# Use [`ssf_custom_bm`](@ref) to calculate the imaginary part of
# ``\mathcal{S}^{2, 3}(ğª, Ï‰) - \mathcal{S}^{3, 2}(ğª, Ï‰)``. In polarized
# neutron scattering, it is conventional to express the 3Ã—3 structure factor
# matrix ``\mathcal{S}^{Î±, Î²}(ğª, Ï‰)`` in the Blume-Maleev polarization axis
# system. Specify the scattering plane ``[0, K, L]`` via the spanning vectors
# ``ğ® = [0, 1, 0]`` and ``ğ¯ = [0, 0, 1]``.
measure = ssf_custom_bm(sys; u=[0, 1, 0], v=[0, 0, 1]) do q, ssf
    imag(ssf[2,3] - ssf[3,2])
end
swt = SpinWaveTheorySpiral(sys; measure, k, axis)
res = intensities(swt, path; energies, kernel=gaussian(fwhm=0.25))
plot_intensities(res; units, saturation=0.8, allpositive=false,
                 title="Im[SÂ²Â³(q, Ï‰) - SÂ³Â²(q, Ï‰)]")
