# # SW15 - Ba‚ÇÉNbFe‚ÇÉSi‚ÇÇO‚ÇÅ‚ÇÑ
#
# This is a Sunny port of [SpinW Tutorial
# 15](https://spinw.org/tutorials/15tutorial), originally authored by Sandor
# Toth. It calculates the linear spin wave theory spectrum of Ba‚ÇÉNbFe‚ÇÉSi‚ÇÇO‚ÇÅ‚ÇÑ.

# Load packages 

using Sunny, GLMakie

# Build a [`Crystal`](@ref) for Ba‚ÇÉNbFe‚ÇÉSi‚ÇÇO‚ÇÅ‚ÇÑ using the crystal
# structure from [Marty et al., Phys. Rev. Lett. **101**, 247201
# (2008)](http://dx.doi.org/10.1103/PhysRevLett.101.247201).

units = Units(:meV, :angstrom)
a = b = 8.539 # (√Ö)
c = 5.2414
latvecs = lattice_vectors(a, b, c, 90, 90, 120)
types = ["Fe", "Nb", "Ba", "Si", "O", "O", "O"]
positions = [[0.24964,0,0.5], [0,0,0], [0.56598,0,0], [2/3,1/3,0.5220],
             [2/3,1/3,0.2162], [0.5259,0.7024,0.3536], [0.7840,0.9002,0.7760]]
langasite = Crystal(latvecs, positions, 150; types)
cryst = subcrystal(langasite, "Fe")
view_crystal(cryst)

# Create a [`System`](@ref) and set Ba‚ÇÉNbFe‚ÇÉSi‚ÇÇO‚ÇÅ‚ÇÑ exchange interactions as
# parametrized in [Loire et al., Phys. Rev. Lett. **106**, 207201
# (2011)](http://dx.doi.org/10.1103/PhysRevLett.106.207201).

sys = System(cryst, (1, 1, 1), [SpinInfo(1; S=5/2, g=2)], :dipole; seed=0)
J‚ÇÅ = 0.85
J‚ÇÇ = 0.24
J‚ÇÉ = 0.053
J‚ÇÑ = 0.017
J‚ÇÖ = 0.24
set_exchange!(sys, J‚ÇÅ, Bond(3, 2, [1,1,0]))
set_exchange!(sys, J‚ÇÑ, Bond(1, 1, [0,0,1]))
set_exchange!(sys, J‚ÇÇ, Bond(1, 3, [0,0,0]))

# The final two exchanges define the chirality of the magnetic structure. The
# crystal chirality, ``\epsilon_T``, the chirality of each triangle, ``œµ_D`` and
# the sense of rotation of the spin helices along ``c``, ``œµ_H``. The three
# chiralities are related by ``œµ_T=œµ_D œµ_H``. We now assign ``J_3`` and ``J_5``
# according to the crystal chirality.

œµD = -1
œµH = +1
œµT = œµD * œµH

if œµT == -1
    set_exchange!(sys, J‚ÇÉ, Bond(2, 3, [-1,-1,1]))
    set_exchange!(sys, J‚ÇÖ, Bond(3, 2, [1,1,1]))
elseif œµT == 1
    set_exchange!(sys, J‚ÇÖ, Bond(2, 3, [-1,-1,1]))
    set_exchange!(sys, J‚ÇÉ, Bond(3, 2, [1,1,1]))
else
    throw("Provide a valid chirality")
end

# This compound is known to have a spiral order with approximate propagation
# wavevector ``ùê§ ‚âà [0, 0, 1/7]``. Search for this magnetic order with
# [`spiral_minimize_energy!`](@ref). Due to reflection symmetry, one of two
# possible propagation wavevectors may appear, ``ùê§ = ¬± [0, 0, 0.1426...]``.
# Note that ``k_z = 0.1426...`` is very close to ``1/7 = 0.1428...``.

axis = [0, 0, 1]
randomize_spins!(sys)
k = spiral_minimize_energy!(sys, axis)

# We can visualize the full magnetic cell using [`repeat_periodically_as_spiral`](@ref),
# which includes 7 rotated copies of the chemical cell.

sys_enlarged = repeat_periodically_as_spiral(sys, (1, 1, 7); k, axis)
plot_spins(sys_enlarged; color=[s[1] for s in sys_enlarged.dipoles])

# One could perform a spin wave calculation using either
# [`SpinWaveTheory`](@ref) on `sys_enlarged`, or [`SpiralSpinWaveTheory`](@ref)
# on the original `sys`. The latter has some restrictions on the interactions,
# but allows for our slightly incommensurate wavevector ``ùê§``.

measure = ssf_perp(sys)
swt = SpiralSpinWaveTheory(sys; measure, k, axis)

# Calculate broadened intensities for a path ``[0, 1, L]`` through reciprocal
# space

qs = [[0, 1, -1], [0, 1, -1+1], [0, 1, -1+2], [0, 1, -1+3]]
path = q_space_path(cryst, qs, 400)
energies = range(0, 6, 400)
res = intensities(swt, path; energies, kernel=gaussian(fwhm=0.25))
axisopts = (; title=L"$œµ_T=-1$, $œµ_Œî=-1$, $œµ_H=+1$", titlesize=20)
plot_intensities(res; units, axisopts, saturation=0.7, colormap=:jet)

# Use [`ssf_custom_bm`](@ref) to calculate the imaginary part of
# ``\mathcal{S}^{2, 3}(ùê™, œâ) - \mathcal{S}^{3, 2}(ùê™, œâ)``. In polarized
# neutron scattering, it is conventional to express the 3√ó3 structure factor
# matrix ``\mathcal{S}^{Œ±, Œ≤}(ùê™, œâ)`` in the Blume-Maleev polarization axis
# system. Specify the scattering plane ``[0, K, L]`` via the spanning vectors
# ``ùêÆ = [0, 1, 0]`` and ``ùêØ = [0, 0, 1]``.
measure = ssf_custom_bm(sys; u=[0, 1, 0], v=[0, 0, 1]) do q, ssf
    imag(ssf[2,3] - ssf[3,2])
end
swt = SpiralSpinWaveTheory(sys; measure, k, axis)
res = intensities(swt, path; energies, kernel=gaussian(fwhm=0.25))
axisopts = (; title=L"$œµ_T=-1$, $œµ_Œî=-1$, $œµ_H=+1$", titlesize=20)
plot_intensities(res; units, axisopts, saturation=0.8, allpositive=false)
