struct ClassicalIntensityFormula{T} <: IntensityFormula
    kT :: Float64
    formfactors :: Union{Nothing, Vector{FormFactor}}
    string_formula :: String
    calc_intensity :: Function
end

function Base.show(io::IO, formula::ClassicalIntensityFormula{T}) where T
    print(io,"ClassicalIntensityFormula{$T}")
end

function Base.show(io::IO, ::MIME"text/plain", formula::ClassicalIntensityFormula{T}) where T
    printstyled(io, "Classical Scattering Intensity Formula\n";bold=true, color=:underline)

    formula_lines = split(formula.string_formula,'\n')

    println(io, "At discrete scattering modes S = S[ix_q,ix_Ï‰], use:")
    println(io)
    print(io, "  Intensity[ix_q,ix_Ï‰] = ")

    intensity_equals = "  Intensity[ix_q,ix_Ï‰] = "
    spacing = repeat(' ', textwidth(intensity_equals))
    println(io, intensity_equals, join(formula_lines, "\n" * spacing))

    if isnothing(formula.formfactors)
        printstyled(io, "No form factors specified\n";color=:yellow)
    else
        printstyled(io, "Form factors included in S âœ“\n";color=:green)
    end
    if formula.kT == Inf
        printstyled(io, "No temperature correction";color=:yellow)
        print(io, " (kT = âˆž)\n")
    else
        printstyled(io, "Temperature corrected (kT = $(formula.kT)) âœ“\n";color = :green)
    end
    if T != Float64
        println(io,"Intensity :: $(T)")
    end
end

"""
    formula = intensity_formula(sc::SampledCorrelations)

Establish a formula for computing the intensity of the discrete scattering modes
`(q,Ï‰)` using the correlation data ``ð’®^{Î±Î²}(q,Ï‰)`` stored in the
[`SampledCorrelations`](@ref). The `formula` returned from `intensity_formula`
can be passed to [`intensities_interpolated`](@ref) or
[`intensities_binned`](@ref).

    intensity_formula(sc,...; kT = Inf, formfactors = ...)

There are keyword arguments providing temperature and form factor corrections:

- `kT`: If a temperature is provided, the intensities will be rescaled by a
    temperature- and Ï‰-dependent classical-to-quantum factor. `kT` should be
    specified when making comparisons with spin wave calculations or
    experimental data. If `kT` is not specified, infinite temperature (no
    correction) is assumed.
- `formfactors`: To apply form factor corrections, provide this keyword with a
    list of `FormFactor`s, one for each symmetry-distinct site in the crystal.
    The order of `FormFactor`s must correspond to the order of site symmetry
    classes, e.g., as they appear when printed in `display(crystal)`.
"""
function intensity_formula(f::Function, sc::SampledCorrelations, corr_ix::AbstractVector{Int64}; 
    kT = Inf, 
    formfactors = nothing, 
    return_type = Float64, 
    string_formula = "f(Q,Ï‰,S{Î±,Î²}[ix_q,ix_Ï‰])"
)
    # If temperature given, ensure it's greater than 0.0
    if kT != Inf
        if iszero(kT)
            error("`kT` must be greater than zero.")
        end
        # Only apply c2q factor if have dynamical correlations
        if isnan(sc.Î”Ï‰)
            error("`kT`-dependent corrections not available when using correlation data generated with `instant_correlations`. Do not set `kT` keyword.")
        end
    end

    Ï‰s_sc = available_energies(sc;negative_energies = true)

    ff_atoms = propagate_form_factors_to_atoms(formfactors, sc.crystal)
    NAtoms = Val(size(sc.data)[2])
    NCorr = Val(length(corr_ix))

    # Intensity is calculated at the discrete (ix_q,ix_Ï‰) modes available to the system.
    # Additionally, for momentum transfers outside of the first BZ, the norm `q_absolute` of the
    # momentum transfer may be different than the one inferred from `ix_q`, so it needs
    # to be provided independently of `ix_q`.
    calc_intensity = function (sc::SampledCorrelations, q_absolute::Vec3, ix_q::CartesianIndex{3}, ix_Ï‰::Int64)
        correlations = phase_averaged_elements(view(sc.data, corr_ix, :, :, ix_q, ix_Ï‰), q_absolute, sc.crystal, ff_atoms, NCorr, NAtoms)

        # This is NaN if sc is instant_correlations
        Ï‰ = (typeof(Ï‰s_sc) == Float64 && isnan(Ï‰s_sc)) ? NaN : Ï‰s_sc[ix_Ï‰] 

        return f(q_absolute, Ï‰, correlations) * classical_to_quantum(Ï‰,kT)
    end
    ClassicalIntensityFormula{return_type}(kT, formfactors, string_formula, calc_intensity)
end

"""
A custom intensity formula can be specifed by providing a function `intensity = f(q,Ï‰,correlations)` and specifying which correlations it requires:

    intensity_formula(f,sc::SampledCorrelations, required_correlations; kwargs...)

The function is intended to be specified using `do` notation. For example, this custom formula sums the off-diagonal correlations:

    required = [(:Sx,:Sy),(:Sy,:Sz),(:Sx,:Sz)]
    intensity_formula(sc,required,return_type = ComplexF64) do k, Ï‰, off_diagonal_correlations
        sum(off_diagonal_correlations)
    end

If your custom formula returns a type other than `Float64`, use the `return_type` keyword argument to flag this.
"""
function intensity_formula(f::Function,sc,required_correlations; kwargs...)
    # SQTODO: This corr_ix may contain repeated correlations if the user does a silly
    # thing like [(:Sx,:Sy),(:Sy,:Sx)], and this can technically be optimized so it's
    # not computed twice
    corr_ix = lookup_correlations(sc.observables,required_correlations)
    intensity_formula(f,sc,corr_ix;kwargs...)
end


function classical_to_quantum(Ï‰, kT::Float64)
    if kT == Inf
        return 1.0
    end
    if Ï‰ > 0
        Ï‰/(kT*(1 - exp(-Ï‰/kT)))
    elseif iszero(Ï‰)
        1.0
    else
        -Ï‰*exp(Ï‰/kT)/(kT*(1 - exp(Ï‰/kT)))
    end
end

"""
    gaussian(; {fwhm, Ïƒ})

Returns the function `exp(-x^2/2Ïƒ^2) / âˆš(2Ï€*Ïƒ^2)`. Exactly one of `fwhm` or `Ïƒ`
must be specified, where `fwhm = (2.355...) * Ïƒ` denotes the full width at half
maximum.
"""
function gaussian(; fwhm=nothing, Ïƒ=nothing)
    if sum(.!isnothing.((fwhm, Ïƒ))) != 1
        error("Exactly one of `fwhm` and `Ïƒ` must be specified.")
    end
    Ïƒ = Float64(@something Ïƒ (fwhm/2âˆš(2log(2))))
    return x -> exp(-x^2/2Ïƒ^2) / âˆš(2Ï€*Ïƒ^2)
end


"""
    integrated_gaussian(; {fwhm, Ïƒ}) 

Returns the function `erf(x/âˆš2Ïƒ)/2`, which is the integral of [`gaussian`](@ref)
over the range ``[0, x]``. Exactly one of `fwhm` or `Ïƒ` must be specified, where
`fwhm = (2.355...) * Ïƒ` denotes the full width at half maximum. Intended for use
with [`intensities_binned`](@ref).
"""
function integrated_gaussian(; fwhm=nothing, Ïƒ=nothing)
    if sum(.!isnothing.((fwhm, Ïƒ))) != 1
        error("Exactly one of `fwhm` and `Ïƒ` must be specified.")
    end
    Ïƒ = Float64(@something Ïƒ (fwhm/2âˆš(2log(2))))
    return x -> erf(x/âˆš2Ïƒ)/2
end

"""
    lorentzian(; fwhm)

Returns the function `(Î“/2) / (Ï€*(x^2+(Î“/2)^2))` where `Î“ = fwhm` is the full
width at half maximum.
"""
function lorentzian(; fwhm)
    Î“ = fwhm
    return x -> (Î“/2) / (Ï€*(x^2+(Î“/2)^2))
end

"""
    integrated_lorentzian(; fwhm) 

Returns the function `atan(2x/Î“)/Ï€`, which is the integral of
[`lorentzian`](@ref) over the range ``[0, x]``, where `Î“ = fwhm` is the full
width at half maximum. Intended for use with [`intensities_binned`](@ref).
"""
function integrated_lorentzian(; fwhm)
    Î“ = fwhm
    return x -> atan(2x/Î“)/Ï€
end


"""
    broaden_energy(sc::SampledCorrelations, vals, kernel::Function; negative_energies=false)

Performs a real-space convolution along the energy axis of an array of
intensities. Assumes the format of the intensities array corresponds to what
would be returned by [`intensities_interpolated`](@ref). `kernel` must be a function that
takes two numbers: `kernel(Ï‰, Ï‰â‚€)`, where `Ï‰` is a frequency, and `Ï‰â‚€` is the
center frequency of the kernel. Sunny provides [`lorentzian`](@ref)
for the most common use case:

```
newvals = broaden_energy(sc, vals, (Ï‰, Ï‰â‚€) -> lorentzian(fwhm=0.2)(Ï‰-Ï‰â‚€))
```
"""
function broaden_energy(sc::SampledCorrelations, is, kernel::Function; negative_energies=false)
    dims = size(is)
    Ï‰vals = available_energies(sc; negative_energies)
    out = zero(is)
    for (Ï‰â‚€i, Ï‰â‚€) in enumerate(Ï‰vals)
        for (Ï‰i, Ï‰) in enumerate(Ï‰vals)
            for qi in CartesianIndices(dims[1:end-1])
                out[qi,Ï‰i] += is[qi,Ï‰â‚€i]*kernel(Ï‰, Ï‰â‚€)*sc.Î”Ï‰
            end
        end
    end
    return out
end
