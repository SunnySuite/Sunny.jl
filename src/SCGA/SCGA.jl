"""
    SCGA(sys::System; measure, kT, dq)

Constructs an object to calculate [`intensities_static`](@ref) within the self
consistent gaussian approximation (SCGA). This theory assumes a classical
Boltzmann distribution with temperature `kT`. It is expected to be meangingful
above the ordering temperature, where fluctuations are approximately Gaussian.

Only `:dipole` and `:dipole_uncorrected` system modes are supported.

The theory of SCGA approximates local spin magnitude constraints with a _weaker_
global constraint condition. For each spin sublattice, the global spin sum rule
can be expressed as an integral over the unit cube ``ðª âˆˆ [0,1]^3`` for
wavevectors ``ðª`` in reciprocal lattice units (RLU). Each such integral will be
approximated as a discrete sum over a regular grid of `floor(1/dq)^3`
wavevectors for the provided `dq` value.

If the conventional crystal cell admits a smaller primitive cell, then the SCGA
calculations can be accelerated. Construct a smaller system with
[`reshape_supercell`](@ref) and [`primitive_cell`](@ref). In this case, the
discretized ``ðª``-point grid runs over the full Brillouin zone associated with
the primitive cell of the crystal.
"""
struct SCGA
    sys :: System
    measure :: MeasureSpec
    Î² :: Float64
    Î»s :: Vector{Float64}

    function SCGA(sys::System; measure::Union{Nothing, MeasureSpec}, kT::Real, dq::Float64)
        sys.dims == (1, 1, 1) || error("System dims must be (1, 1, 1).")

        measure = @something measure empty_measurespec(sys)
        if size(eachsite(sys)) != size(measure.observables)[2:5]
            error("Size mismatch. Check that measure is built using consistent system.")
        end

        if !(sys.mode in (:dipole, :dipole_uncorrected))
            error("SCGA requires :dipole or :dipole_uncorrected mode.")
        end

        kT > 0 || error("Temperature kT must be positive")
        Î² = 1 / kT

        0 < dq < 1 || error("Select q-space resolution 0 < dq < 1.")
        qs = make_q_grid(sys, dq)
        Js = [fourier_exchange_matrix(sys; q) for q in qs]

        # Initial guess for the Lagrange multipliers in the physically allowed
        # space (all shifted J matrices positive definite). If `eigmin` becomes
        # a bottleneck, we can try weaker bounds using Gershgorin's circle
        # theorem: https://en.wikipedia.org/wiki/Gershgorin_circle_theorem.
        Î»_init = -minimum(eigmin(J) for J in Js) + 1/Î²

        Î»s = if allequal(sys.crystal.classes)
            find_lagrange_multiplier_single(sys, Js, Î², Î»_init)
        else
            find_lagrange_multiplier_multi(sys, Js, Î², Î»_init)
        end

        return new(sys, measure, Î², Î»s)
    end
end

function make_q_grid(sys, dq)
    wraps = [false, false, false]
    for i in 1:natoms(sys.crystal)
        for coupling in sys.interactions_union[i].pair
            (; isculled, bond) = coupling
            isculled && break
            @. wraps = wraps || !iszero(bond.n)
        end
    end

    qÎ± = [w ? (-1/2 : dq : 1/2-dq) : [0] for w in wraps]
    return vec([to_standard_rlu(sys, Vec3(q_reshaped)) for q_reshaped in Iterators.product(qÎ±...)])
end

# Computes the Lagrange multiplier for the standard SCGA approach with a common
# Lagrange multiplier for all sublattices.
function find_lagrange_multiplier_single(sys, Js, Î², Î»_init)
    evals = Iterators.flatten(eigvals(J) for J in Js)
    sÂ² = norm2(sys.Îºs) * length(Js)

    function fgh!(_, gbuffer, hbuffer, Î»s)
        Î» = Î»s[1]
        # Î» must be large enough to shift all eigenvalues positive. Otherwise,
        # apply an infinite penalty.
        if Î» + minimum(evals) <= 0
            isnothing(gbuffer) || gbuffer .= NaN
            isnothing(hbuffer) || hbuffer .= NaN
            return Inf
        end
        fbuffer = Î»*sÂ²/2 - sum(log(Î» + ev) for ev in evals) / 2Î²
        if !isnothing(gbuffer)
            gbuffer[1] = sÂ²/2 - sum(1 / (Î» + ev) for ev in evals) / 2Î²
        end
        if !isnothing(hbuffer)
            hbuffer[1, 1] = sum(1 / (Î» + ev)^2 for ev in evals) / 2Î²
        end
        return fbuffer
    end

    Î»s = newton_with_backtracking(fgh!, [Î»_init]; x_reltol=1e-10, show_trace=false)
    return fill(Î»s[1], natoms(sys.crystal))
end


function find_lagrange_multiplier_multi(sys, Js, Î², Î»_init)
    Na = natoms(sys.crystal)
    sÂ² = vec(sys.Îºs .^ 2)

    function fgh!(_, gbuffer, hbuffer, Î»s)
        fbuffer = 0.0
        if !isnothing(gbuffer)
            gbuffer .= 0
        end
        if !isnothing(hbuffer)
            hbuffer .= 0
        end

        Î› = Diagonal(repeat(Î»s, inner=3))
        A = zeros(ComplexF64, 3Na, 3Na)
        Aâ»Â¹ = zeros(ComplexF64, 3, Na, 3, Na)

        # Determine the Lagrange multipliers Î» by maximizing (not minimizing!)
        # the "grand" free energy G(Î») = log det A / 2Î² - âˆ‘áµ¢ Î»áµ¢ sÂ²áµ¢ / 2, where A
        # = J + Î›. Implement this numerically as minimization of the objective
        # function f = -G.
        for J in Js
            # Cholesky decomposition fails if the matrix A is not positive
            # definite. This implies unphysical Î» values, which we penalize by
            # making the objective function infinite.
            @. A = J + Î›
            A_chol = cholesky!(A, RowMaximum(); check=false)
            if !issuccess(A_chol)
                isnothing(gbuffer) || gbuffer .= NaN
                isnothing(hbuffer) || hbuffer .= NaN
                return Inf
            end

            ldiv!(reshape(Aâ»Â¹, 3Na, 3Na), A_chol, I(3Na))

            # The finite-valued objective function f
            fbuffer += (Î»s' * sÂ²) / 2 - logdet(A_chol) / 2Î²

            # Gradient of f
            if !isnothing(gbuffer)
                for i in 1:Na
                    gbuffer[i] += sÂ²[i] / 2 - real(tr(view(Aâ»Â¹, :, i, :, i))) / 2Î²
                end
            end

            # Hessian of f
            if !isnothing(hbuffer)
                for i in 1:Na, j in 1:Na
                    hbuffer[i, j] += + norm(view(Aâ»Â¹, :, i, :, j))^2 / 2Î²
                end
            end
        end

        return fbuffer
    end

    Î»s = fill(Î»_init, Na)
    return newton_with_backtracking(fgh!, Î»s; x_reltol=1e-10, show_trace=false)
end


function intensities_static(scga::SCGA, qpts)
    (; sys, measure, Î»s, Î²) = scga
    Î› = Diagonal(repeat(Î»s, inner=3))

    qpts = convert(AbstractQPoints, qpts)
    cryst = orig_crystal(scga.sys)

    Na = nsites(sys)
    Ncells = Na / natoms(cryst)
    Nq = length(qpts.qs)

    # Temporary storage for pair correlations
    Nobs = num_observables(measure)
    Ncorr = num_correlations(measure)
    corrbuf = zeros(ComplexF64, Ncorr)

    # Preallocation
    A = zeros(ComplexF64, 3Na, 3Na)
    O = view(measure.observables::Array{Vec3, 5}, :, 1, 1, 1, :)
    X = zeros(ComplexF64, 3Na, Nobs)
    pref = zeros(ComplexF64, 3Na, Nobs)
    pref_reshaped = reshape(pref, 3, Na, Nobs)
    intensity = zeros(eltype(measure), Nq)

    for (iq, q) in enumerate(qpts.qs)
        q_global = cryst.recipvecs * q

        for i in 1:Na, Î¼ in 1:Nobs
            r_global = global_position(sys, (1, 1, 1, i)) # + offsets[Î¼, i]
            ff = get_swt_formfactor(measure, Î¼, i)
            c = exp(+ im * dot(q_global, r_global)) * compute_form_factor(ff, norm2(q_global))
            for Î± in 1:3
                pref_reshaped[Î±, i, Î¼] = c * O[Î¼, i][Î±]
            end
        end

        fourier_exchange_matrix!(A, sys; q)
        A .+= Î›
        A .*= Î²
        ldiv!(X, cholesky!(A), pref)
        map!(corrbuf, measure.corr_pairs) do (Î¼, Î½)
            return dot(view(pref, :, Î¼), view(X, :, Î½)) / Ncells
        end

        #=
        Aâ»Â¹ = reshape(inv(Î²*Î› + Î²*Jq), 3, Na, 3, Na)
        map!(corrbuf, measure.corr_pairs) do (Î¼, Î½)
            acc = zero(ComplexF64)
            for Î± in 1:3, Î² in 1:3, i in 1:Na, j in 1:Na
                acc += conj(pref[Î±, i, Î¼]) * Aâ»Â¹[Î±, i, Î², j] * pref[Î², j, Î½]
            end
            return acc / Ncells
        end
        =#

        intensity[iq] = measure.combiner(q_global, corrbuf)
    end

    return StaticIntensities(sys.crystal, qpts, reshape(intensity, size(qpts.qs)))
end
