"""
    StaticCorrelationsSCGA(sys::System; measure, kT, dq)

Constructs an object to calculate [`intensities_static`](@ref) within the self
consistent gaussian approximation (SCGA). This theory assumes a classical
Boltzmann distribution with temperature `kT`. It is expected to be meangingful
above the ordering temperature, where fluctuations are approximately Gaussian.

Only `:dipole` and `:dipole_uncorrected` system modes are supported.

The theory of SCGA approximates local spin magnitude constraints with a _weaker_
global constraint condition. For each spin sublattice, the global spin sum rule
can be expressed as an integral over the unit cube ``ðª âˆˆ [0,1]^3`` for
wavevectors ``ðª`` in reciprocal lattice units (RLU). Each such integral will be
approximated as a discrete sum over a regular grid of `floor(1/dq)^3`
wavevectors for the provided `dq` value.

If the conventional crystal cell admits a smaller primitive cell, then the SCGA
calculations can be accelerated. Construct a smaller system with
[`reshape_supercell`](@ref) and [`primitive_cell`](@ref). In this case, the
discretized ``ðª``-point grid runs over the full Brillouin zone associated with
the primitive cell of the crystal.
"""
struct StaticCorrelationsSCGA
    sys :: System
    measure :: MeasureSpec
    Î² :: Float64
    Î»s :: Vector{Float64}

    function StaticCorrelationsSCGA(sys::System; measure::Union{Nothing, MeasureSpec}, kT::Float64, dq::Float64)
        measure = @something measure empty_measurespec(sys)
        if size(eachsite(sys)) != size(measure.observables)[2:5]
            error("Size mismatch. Check that measure is built using consistent system.")
        end

        kT > 0 || error("Temperature kT must be positive")
        Î² = 1 / kT

        qs = make_q_grid(sys, dq)
        Js = [fourier_exchange_matrix(sys; q) for q in qs]

        sublattice_resolved = !allequal(sys.crystal.classes)
        if sublattice_resolved
            Î»s = find_lagrange_multiplier_opt_sublattice(sys, Js, Î²)
        else
            Î» = find_lagrange_multiplier(sys, Js, Î²)
            Î»s = fill(Î», natoms(sys.crystal))
        end

        return new(sys, measure, Î², Î»s)
    end
end

function make_q_grid(sys, dq)
    wraps = [false, false, false]
    for i in 1:natoms(sys.crystal)
        for coupling in sys.interactions_union[i].pair
            (; isculled, bond) = coupling
            isculled && break
            @. wraps = wraps || !iszero(bond.n)
        end
    end

    qÎ± = [w ? (-1/2 : dq : 1/2-dq) : [0] for w in wraps]
    return vec([to_standard_rlu(sys, Vec3(q_reshaped)) for q_reshaped in Iterators.product(qÎ±...)])
end

# Computes the Lagrange multiplier for the standard SCGA approach with a common
# Lagrange multiplier for all sublattices.
function find_lagrange_multiplier(sys, Js, Î²)
    starting_offset = 0.2
    maxiters = 500
    tol = 1e-10

    evals = Iterators.flatten(eigvals(J) for J in Js)

    Nq = length(Js)
    sÂ² = norm2(sys.Îºs)

    function f(Î»)
        return sum(1 / (Î» + ev) for ev in evals) / (Î² * Nq)
    end
    function J(Î»)
        return -sum(1 / (Î» + ev)^2 for ev in evals) / (Î² * Nq)
    end

    Î»n = starting_offset*0.1/Î² - minimum(evals)
    for n in 1:maxiters
        Î» = Î»n + (1/J(Î»n))*(sÂ²-f(Î»n))
        if abs(Î»-Î»n) < tol
            println("Newton's method converged to within tolerance, $tol, after $n steps.")
            return Î»
        else
            Î»n = Î»
        end
    end
end


function find_lagrange_multiplier_opt_sublattice(sys, Js, Î²; rtol=1e-10)
    Na = natoms(sys.crystal)
    sÂ² = vec(sys.Îºs .^ 2)

    function fgh!(_, gbuffer, hbuffer, Î»s)
        fbuffer = 0.0
        if !isnothing(gbuffer)
            gbuffer .= 0
        end
        if !isnothing(hbuffer)
            hbuffer .= 0
        end

        Î› = diagm(repeat(Î»s, inner=3))

        for J in Js
            A = Î² * (J + Î›)
            T = eigen(A)
            eig_vals = T.values
            U = T.vectors
            Aâ»Â¹ = U * Diagonal(inv.(eig_vals)) * U'
            Aâ»Â¹ = reshape(Aâ»Â¹, 3, Na, 3, Na)

            # Determine Lagrange multipliers Î» by minimizing f, the negative of
            # the "grand" free energy G(Î»), involving Legendre transform into
            # the Î» variables. Physical eigenvalues of each (J + Î›) matrix must
            # be positive, otherwise we apply an infinite penalty to the
            # objective function.
            if minimum(eig_vals) < 0
                fbuffer = Inf
            else
                fbuffer += Î»s' * sÂ² / 2 - (1/2Î²) * sum(log.(eig_vals))
            end

            # Gradient of f
            if !isnothing(gbuffer)
                for i in 1:Na
                    gbuffer[i] += sÂ²[i] / 2 - real(tr(view(Aâ»Â¹, :, i, :, i))) / 2
                end
            end

            # Hessian of f
            if !isnothing(hbuffer)
                for i in 1:Na, j in 1:Na
                    hbuffer[i, j] += + (Î²/2) * norm(view(Aâ»Â¹, :, i, :, j))^2
                end
            end
        end

        return fbuffer
    end

    # Get initial guess for the Lagrange multipliers
    eigmin, eigmax = extrema(Iterators.flatten(eigvals(J) for J in Js))
    Î»_init = -eigmin + (eigmax - eigmin) / 2
    Î»s = Î»_init*ones(Float64, Na)

    return newton_with_backtracking(fgh!, Î»s; f_reltol=1e-12, maxiters=20, show_trace=false)

#=
    options = Optim.Options(; iterations=maxiters, show_trace=false, g_tol)
    result = Optim.optimize(Optim.only_fgh!(fgh!), Î»s, Optim.Newton(), options)
    println(result)
    return real.(Optim.minimizer(result))
=#

end


function intensities_static(scga::StaticCorrelationsSCGA, qpts)
    (; sys, measure, Î»s, Î²) = scga
    Î› = Diagonal(repeat(Î»s, inner=3))

    qpts = convert(AbstractQPoints, qpts)
    cryst = orig_crystal(scga.sys)

    Na = nsites(sys)
    Ncells = Na / natoms(cryst)
    Nq = length(qpts.qs)

    Nobs = num_observables(measure)
    Ncorr = length(measure.corr_pairs)
    corrbuf = zeros(ComplexF64, Ncorr)

    intensity = zeros(eltype(measure), Nq)

    r = sys.crystal.positions

    for (iq, q) in enumerate(qpts.qs)
        pref = zeros(ComplexF64, Nobs, Na)
        q_reshaped = to_reshaped_rlu(sys, q)
        q_global = cryst.recipvecs * q
        for i in 1:Na, Î¼ in 1:3
            ff = get_swt_formfactor(measure, Î¼, i)
            pref[Î¼, i] = exp(-2Ï€ * im * dot(q_reshaped, r[i])) * compute_form_factor(ff, norm2(q_global))
        end
        Jq = fourier_exchange_matrix(sys; q)
        inverted_matrix = inv(Î²*Î› + Î²*Jq) # this is [(IÎ»+J(q))^-1]^Î±Î²_Î¼Î½
        inverted_matrix = reshape(inverted_matrix, 3, Na, 3, Na)

        ssf = zero(CMat3)
        for i in 1:Na, j in 1:Na
            ssf += pref[1, i] * conj(pref[1, j]) * view(inverted_matrix, :, i, :, j)
        end

        @assert ssf â‰ˆ ssf'
        @assert all(>=(0), real(diag(ssf)))

        map!(corrbuf, measure.corr_pairs) do (Î±, Î²)
            ssf[Î±, Î²] / Ncells
        end
        intensity[iq] = measure.combiner(q_global, corrbuf)
    end

    return StaticIntensities(sys.crystal, qpts, reshape(intensity, size(qpts.qs)))
end
