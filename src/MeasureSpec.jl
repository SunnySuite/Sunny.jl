# Op is the type of a local observable operator. Either a Vec3 (for :dipole
# mode, in which case the observable is `opâ‹…S`) or a HermitianC64 (for :SUN
# mode, in which case op is an NÃ—N matrix).
struct MeasureSpec{Op <: Union{Vec3, HermitianC64}, F, Ret}
    observables :: Array{Op, 5}          # (nobs Ã— latsize Ã— natoms)
    corr_pairs :: Vector{NTuple{2, Int}} # (ncorr)
    combiner :: F                        # (q::Vec3, obs) -> Ret

    # TODO: Default combiner will be SVector?
    function MeasureSpec(observables::Array{Op, 5}, corr_pairs, combiner::F) where {Op, F}
        # Lift return type of combiner function to type-level
        Ret = only(Base.return_types(combiner, (Vec3, Vector{ComplexF64})))
        @assert isbitstype(Ret)
        return new{Op, F, Ret}(observables, corr_pairs, combiner)
    end
end

Base.eltype(::MeasureSpec{Op, F, Ret}) where {Op, F, Ret} = Ret


function empty_measurespec(sys)
    observables = zeros(Vec3, 0, size(eachsite(sys))...)
    corr_pairs = NTuple{2, Int}[]
    combiner = (_, _) -> 0.0
    return MeasureSpec(observables, corr_pairs, combiner)
end

function all_dipole_observables(sys::System{0}; apply_g)
    observables = zeros(Vec3, 3, size(eachsite(sys))...)
    for site in eachsite(sys)
        # Component Î± of observable is opâ‹…S = g[Î±,Î²] S[Î²]. Minus sign would
        # cancel because observables come in pairs.
        op = apply_g ? sys.gs[site] : Mat3(I)
        for Î± in 1:3
            observables[Î±, site] = op[Î±, :]
        end
    end
    return observables
end

function all_dipole_observables(sys::System{N}; apply_g) where {N}
    observables = Array{HermitianC64, 5}(undef, 3, size(eachsite(sys))...)
    for site in eachsite(sys)
        S = spin_matrices_of_dim(; N=sys.Ns[site])
        op = apply_g ? sys.gs[site]*S : S
        for Î± in 1:3
            observables[Î±, site] = op[Î±]
        end
    end
    return observables
end


"""
    ssf_custom(f, sys::System; apply_g=true)

Specify measurement of the spin structure factor with a custom contraction
function `f`. This function accepts a wavevector ``ğª`` in global Cartesian
coordinates, and a 3Ã—3 matrix with structure factor intensity components
``\\mathcal{S}^{Î±Î²}(ğª,Ï‰)``. Indices ``(Î±, Î²)`` denote dipole components in
global coordinates. The return value of `f` can be any number or `isbits` type.
With specific choices of `f`, one can obtain measurements such as defined in
[`ssf_perp`](@ref) and [`ssf_trace`](@ref).

By default, the g-factor or tensor is applied at each site, such that the
structure factor components are correlations between the magnetic moment
operators. Set `apply_g = false` to measure correlations between the bare spin
operators.

Intended for use with [`SpinWaveTheory`](@ref) and instances of
[`SampledCorrelations`](@ref).

# Examples

```julia
# Measure all 3Ã—3 structure factor components Sáµ…áµ
measure = ssf_custom((q, ssf) -> ssf, sys)

# Measure the structure factor trace Sáµ…áµ…
measure = ssf_custom((q, ssf) -> real(sum(ssf)), sys)

# Measure imaginary part of SÊ¸á¶» - Sá¶»Ê¸ in the Blume-Maleev coordinate system
# for incident momentum [0, 1, 0] in RLU.
q_i = cryst.recipvecs * [0, 1, 0]
measure = ssf_custom(sys) do q, ssf
    (x, y, z) = blume_maleev(q_i, q)
    imag(y'*ssf*z - z'*ssf*y)
end
```

See also the Sunny documentation on [Structure Factor Calculations](@ref) for
more details.
"""
function ssf_custom(f, sys::System; apply_g=true)
    observables = all_dipole_observables(sys; apply_g)
    corr_pairs = [(3,3), (2,3), (1,3), (2,2), (1,2), (1,1)]
    combiner(q, data) = f(q, SA[
        data[6]       data[5]       data[3]
        conj(data[5]) data[4]       data[2]
        conj(data[3]) conj(data[2]) data[1]
    ])
    return MeasureSpec(observables, corr_pairs, combiner)
end

"""
    ssf_perp(sys::System; apply_g=true)

Specify measurement of the spin structure factor with contraction by
``(I-ğªâŠ—ğª/q^2)``. The contracted value provides an estimate of unpolarized
scattering intensity. In the singular limit ``ğª â†’ 0``, the contraction matrix
is replaced by its rotational average, ``(2/3) I``.

See also [`ssf_trace`](@ref) and [`ssf_custom`](@ref).
"""
function ssf_perp(sys::System; apply_g=true)
    return ssf_custom(sys; apply_g) do q, ssf
        q2 = norm2(q)
        # Imaginary part vanishes in symmetric contraction
        ssf = real(ssf)
        # "S-perp" contraction matrix (1 - qâŠ—q/qÂ²) appropriate to unpolarized
        # neutrons. In the limit q â†’ 0, use (1 - qâŠ—q/qÂ²) â†’ 2/3, which
        # corresponds to a spherical average over uncorrelated data:
        # https://github.com/SunnySuite/Sunny.jl/pull/131
        (iszero(q2) ? (2/3)*tr(ssf) : tr(ssf) - dot(q, ssf, q) / q2)
    end
end

"""
    ssf_trace(sys::System; apply_g=true)

Specify measurement of the spin structure factor, with trace over spin
components. This quantity can be useful for checking quantum sum rules.

See also [`ssf_perp`](@ref) and [`ssf_custom`](@ref).
"""
function ssf_trace(sys::System{N}; apply_g=true) where N
    return ssf_custom(sys; apply_g) do q, ssf
        tr(real(ssf))
    end
end
