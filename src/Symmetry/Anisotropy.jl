# Return a matrix whose columns are an orthogonal basis for the span of columns
# in A. Adapted from LinearAlgebra.nullspace().
function colspace(A::AbstractVecOrMat; atol::Real)
    m, n = size(A, 1), size(A, 2)
    (m == 0 || n == 0) && return A
    SVD = svd(A)
    indices = findall(>(atol), SVD.S)
    return copy(SVD.U[:, indices])
end

function axis_angle(R::Mat3)
    # Assertion disabled for performance
    # @assert R'*R â‰ˆ I && det(R) â‰ˆ 1

    # Formula derived by Mike Day, Insomniac Games, and posted online as
    # "Converting a Rotation Matrix to a Quaternion".
    # https://d3cw3dd2w32x2b.cloudfront.net/wp-content/uploads/2015/01/matrix-to-quat.pdf
    (m00, m10, m20, m01, m11, m21, m02, m12, m22) = R[:]
    if (m22 < 0)
        if (m00 > m11)
            t = 1 + m00 - m11 - m22
            q = SA[t, m01+m10, m20+m02, m12-m21]
        else
            t = 1 - m00 + m11 - m22
            q = SA[m01+m10, t, m12+m21, m20-m02]
        end
    else
        if (m00 < -m11)
            t = 1 - m00 - m11 + m22
            q = SA[m20+m02, m12+m21, t, m01-m10]
        else
            t = 1 + m00 + m11 + m22
            q = SA[m12-m21, m20-m02, m01-m10, t]
        end
    end

    # Construct a unit quaternion
    q *= 0.5 / sqrt(t)

    # Angle of rotation
    q4 = max(min(q[4], 1.0), -1.0)
    Î¸ = 2acos(q4)

    if Î¸ < 1e-12
        # Axis is ill-defined for the identity matrix, but we don't want NaNs
        n = Vec3(0, 0, 0)
    else
        # Standard conversion from a unit quaternion q to an axis-angle
        n = q[1:3] / sqrt(1 - q[4]^2)
    end

    # Negate the axis to invert the rotation. This effectively achieves a
    # transpose in R, which allows us to interpret R as right-multiplication on
    # column vectors (the note by Mike Day viewed R as left-multiplication on
    # row vectors).
    n = -n

    return (n, Î¸)
end

function unitary_for_rotation(N::Int, R::Mat3)
    !(R'*R â‰ˆ I)   && error("Not an orthogonal matrix, R = $R.")
    !(det(R) â‰ˆ 1) && error("Not a rotation matrix, R = $R.")
    S = gen_spin_ops(N)
    n, Î¸ = axis_angle(R)
    return exp(-im*Î¸*(n'*S))
end

function rotate_operator(A::Matrix, R)
    R = convert(Mat3, R)
    N = size(A, 1)
    U = unitary_for_rotation(N, R)
    return U'*A*U
end

function rotate_operator(P::DP.AbstractPolynomialLike, R)
    R = convert(Mat3, R)

    # The spin operator vector rotates two equivalent ways:
    #  1. S_Î± -> U' S_Î± U
    #  2. S_Î± -> R_Î±Î² S_Î²
    #
    # where U = exp(-i Î¸ nâ‹…S), for the axis-angle rotation (n, Î¸). Apply the
    # latter to transform symbolic spin operators.
    ğ’®â€² = R * ğ’®

    # Spherical tensors T_q rotate two equivalent ways:
    #  1. T_q -> U' T_q U       (with U = exp(-i Î¸ nâ‹…S) in dimension N irrep)
    #  2. T_q -> D*_{q,qâ€²} T_qâ€² (with D = exp(-i Î¸ nâ‹…S) in dimension 2k+1 irrep)
    #
    # The Stevens operators ğ’ª_q are linearly related to T_q via ğ’ª = Î± T.
    # Therefore rotation on Stevens operators is ğ’ª -> Î± conj(D) Î±â»Â¹ ğ’ª.
    local ğ’ª = stevens_operator_symbols
    ğ’ªâ€² = map(ğ’ª) do ğ’ªâ‚–
        k = Int((length(ğ’ªâ‚–)-1)/2)
        D = unitary_for_rotation(2k+1, R)
        R_stevens = stevens_Î±[k] * conj(D) * stevens_Î±inv[k]
        @assert norm(imag(R_stevens)) < 1e-12
        real(R_stevens) * ğ’ªâ‚–
    end

    # Spin squared as a scalar may be introduced through
    # operator_to_classical_stevens()
    X = spin_squared_symbol

    # Perform substitutions
    Pâ€² = P(ğ’® => ğ’®â€², [ğ’ª[k] => ğ’ªâ€²[k] for k=1:6]..., X => X)

    # Remove terms very near zero
    return DP.mapcoefficients(Pâ€²) do c
        abs(c) < 1e-12 ? zero(c) : c
    end
end

# Coefficients Î± to convert from spherical tensors to Stevens operators. For
# each k, the mapping is ğ’ª_q = Î±_{q,q'} T_q'. Spherical tensors T use the
# normalization convention of Koster and Statz (1959) and Buckmaster et al
# (1972) operator (KS/BCS). An explicit construction of T is given by
# spherical_tensors() in test_symmetry.jl . The operators ğ’ª can also be
# expressed as explicit polynomials of spin operators, as in
# stevens_abstract_polynomials() below.
const stevens_Î± = let
    # These coefficients for a[k,q] were taken from Table 1 of C. Rudowicz, J.
    # Phys. C: Solid State Phys. 18, 1415 (1985). It appears the general formula
    # could be unraveled from Eq. (21) of I. D. Ryabov, J. Magnetic Resonance
    # 140, 141-145 (1999).
    a = [1     1/âˆš2     0        0        0        0    0;
         âˆš6    1/2      1        0        0        0    0;
         âˆš10   âˆš(10/3)  1/âˆš3     âˆš2       0        0    0;
         2âˆš70  âˆš(7/2)   âˆš7       1/âˆš2     2        0    0;
         6âˆš14  2âˆš(21/5) âˆš(3/5)   6âˆš(2/5)  2/âˆš5     2âˆš2  0;
         4âˆš231 âˆš22      4âˆš(11/5) 2âˆš(11/5) 4âˆš(11/6) 2/âˆš3 4;]
    a = OffsetArray(a, 1:6, 0:6)

    ret = Matrix{ComplexF64}[]

    for k = 1:6
        sz = 2k+1
        Î± = zeros(ComplexF64, sz, sz)

        for q = 0:k
            # Convert q and -q into array indices. The convention is descending
            # order, q = k...-k.
            qi = k - (+q) + 1
            qÌ„i = k - (-q) + 1

            # Fill Î±_{Â±q,Â±q} values
            if q == 0
                Î±[qi, qi] = a[k,q]
            else
                Î±[qi, qÌ„i] =                 a[k, q]
                Î±[qi, qi] =        (-1)^q * a[k, q]
                Î±[qÌ„i, qÌ„i] =   im *          a[k, q]
                Î±[qÌ„i, qi] = - im * (-1)^q * a[k, q]
            end
        end
        push!(ret, Î±)
    end

    ret
end

const stevens_Î±inv = map(inv, stevens_Î±)


# Calculate coefficients c that satisfy `báµ€ ğ’ª = cáµ€ T`, where ğ’ª are the Stevens
# operators, and T are the spherical harmonics. Using `ğ’ª = Î± T`, we must solve
# báµ€ Î± = cáµ€, or c = Î±áµ€ b.
function transform_stevens_to_spherical_coefficients(k, b)
    return transpose(stevens_Î±[k]) * b
end


# Calculate coefficients b that satisfy `báµ€ ğ’ª = cáµ€ T`, where ğ’ª are the Stevens
# operators, and T are the spherical harmonics. Using `ğ’ª = Î± T`, we must solve
# báµ€ Î± = cáµ€, or b = Î±â»áµ€ c.
function transform_spherical_to_stevens_coefficients(k, c)
    return transpose(stevens_Î±inv[k]) * c
end


function basis_for_symmetry_allowed_anisotropies(cryst::Crystal, i::Int; k::Int, R=Mat3(I))
    # The symmetry operations for the point group at atom i. Each one encodes a
    # rotation/reflection.
    symops = symmetries_for_pointgroup_of_atom(cryst, i)

    # The Wigner D matrices for each symop
    Ds = map(symops) do s
        # R is an orthogonal matrix that transforms positions, x â†’ xâ€² = R x. It
        # might or might not include a reflection, i.e., det R = Â±1.
        sR = cryst.lat_vecs * s.R * inv(cryst.lat_vecs)

        # TODO: If the crystal unit cell is imperfect, then R will only be
        # orthogonal up to some tolerance cryst.symprec, whereas subsequent symmetry
        # analysis assumes atol=1e-12. To make R orthogonal up to numerical
        # precision, we should use spglib's feature "spg_standardize_cell()".

        # Unlike position x, spin S = [Sx, Sy, Sz] is a _pseudo_ vector, which
        # means that, under reflection, the output gains an additional minus
        # sign. That is, the orthogonal transformation R applied to spin has the
        # action, S â†’ Sâ€² = Â± R S, where the minus sign corresponds to the case
        # det(R) = -1. More simply, we may write Sâ€² = Q S, where Q = det(R) R.
        Q = det(sR) * sR

        # The Wigner D matrix, whose action on a spherical tensor corresponds to
        # the 3x3 rotation Q (see more below).
        return unitary_for_rotation((2k+1), Q)
    end
    
    # A general operator in the spin-k representation can be decomposed in the
    # basis of spherical tensors, ğ’œ = âˆ‘_q c_q T_kq, for some coefficients c_q.
    # Spherical tensors transform as T_kq â†’ D^{*}_qqâ€² T_kqâ€². Alternatively, we
    # can treat T_kq as invariant, and calculate the transformed ğ’œ as a
    # transformation of the coefficients c â†’ câ€² = Dâ€  c. Given arbitrary eáµ¢, the
    # operator represented by coefficients cáµ¢ = (Dâ‚â€  + Dâ‚‚â€  + ... Dâ‚™â€ ) eáµ¢ is
    # invariant to all point group operations, i.e., Djâ€  cáµ¢ = cáµ¢. Repeating this
    # procedure for a complete basis {eâ‚, eâ‚‚, ...}, we determine all
    # symmetry-invariant operators ğ’œ. Specifically, every column of the matrix
    # C = (Dâ‚â€  + Dâ‚‚â€  + ... Dâ‚™â€ ) gives coefficients to a symmetry-invariant
    # operator ğ’œ.
    C = sum(D' for D in Ds)

    # Transform coefficients c to câ€² in rotated Stevens operators, Tâ€² = D* T,
    # where the Wigner D matrix is associated with the rotation R. That is, find
    # câ€² satisfying câ€²áµ€ Tâ€² = c T. Recall câ€²áµ€ Tâ€² = (câ€²áµ€ D*) T = (Dâ€  câ€²)áµ€ T. The
    # constraint becomes Dâ€  câ€² = c. Since D is unitary, we have câ€² = D c. We
    # apply this transformation to each column c of C.
    D = unitary_for_rotation(2k+1, convert(Mat3, R))
    C = D * C

    # Find an orthonormal basis for the columns of A, discarding linearly
    # dependent columns.
    C = colspace(C; atol=1e-12)

    # It is tempting to sparsify here to make the ouput look nicer. Don't do
    # this because (empirically) it is observed to significantly degrade
    # accuracy in stevens_basis_for_symmetry_allowed_anisotropies().

    # C = sparsify_columns(C; atol=1e-12)

    return C
end

function stevens_basis_for_symmetry_allowed_anisotropies(cryst::Crystal, i::Int; k::Int, R=Mat3(I))
    # Each column of C represents a coefficient vector c that can be contracted
    # with spherical tensors T to realize an allowed anisotropy, Î› = cáµ€ T.
    C = basis_for_symmetry_allowed_anisotropies(cryst, i; k, R)

    # Transform each column c to coefficients b that satisfy báµ€ ğ’ª = cáµ€ T
    B = [transform_spherical_to_stevens_coefficients(k, c) for c in eachcol(C)]

    # Concatenate columns into single matrix
    B = reduce(hcat, B; init=zeros(ComplexF64, 2k+1,0))
    
    # Find linear combination of columns that sparsifies B
    B = sparsify_columns(B; atol=1e-12)

    # All coefficients must now be real
    @assert norm(imag(B)) < 1e-12
    B = real(B)

    return B
end


function is_anisotropy_valid(cryst::Crystal, i::Int, Î›)
    symops = symmetries_for_pointgroup_of_atom(cryst, i)

    for s in symops
        R = cryst.lat_vecs * s.R * inv(cryst.lat_vecs)
        Î›â€² = rotate_operator(Î›, det(R)*R)
        if !(Î›â€² â‰ˆ Î›)
            return false
        end
    end
    return true
end


# Subject to change. Users should call print_suggested_frame() instead
function suggest_frame_for_atom(cryst::Crystal, i::Int)
    # Collect list of symmetry axes along with their counts
    axes_counts = Tuple{Vec3, Int}[]
    symops = symmetries_for_pointgroup_of_atom(cryst, i)
    for s in symops
        # Not interested in the identity, nor pure inversions
        (s.R â‰ˆ I || s.R â‰ˆ -I) && continue

        # Orthogonal transformation for pointgroup symmetry
        R = cryst.lat_vecs * s.R * inv(cryst.lat_vecs)

        # Extract normalized vector n, either a rotation axis or the normal of a
        # reflection plane
        Q = det(R) * R
        n, _ = axis_angle(Q)

        # Prefer positive coordinates
        if sum(n) < 0
            n = -n
        end

        # Collect all unique axes, along with their counts. We compare against
        # the director n*n' to be insensitive to sign, n â†’ -n.
        i = findfirst(x -> x[1]*x[1]' â‰ˆ n*n', axes_counts)
        if isnothing(i)
            push!(axes_counts, (n, 1))
        else
            (nâ€², cnt) = axes_counts[i]
            axes_counts[i] = (nâ€², cnt+1)
        end
    end

    if isempty(axes_counts)
        println("Warning: Could not find a symmetry axis.")
        return Mat3(I)
    end

    function select_axis(axes_counts)
        # Candidates are those with maximimal symmetry
        max_count = maximum(x -> x[2], axes_counts)
        candidates = [x[1] for x in axes_counts if (x[2] == max_count)]

        # Choose according to aesthetic heuristics
        return argmin(candidates) do n
            # Standard axis (x, y, or z) is preferred
            n â‰ˆ Vec3(0,0,1) && return 0
            n â‰ˆ Vec3(1,0,0) && return 1
            n â‰ˆ Vec3(0,1,0) && return 2
        
            # Look for [1,1,1] axis
            n â‰ˆ Vec3(1,1,1)/âˆš3 && return 3
        
            # Look for [Â±1,Â±1,Â±1] axis
            abs.(n) â‰ˆ Vec3(1,1,1)/âˆš3 && return 4
        
            # Try to minimize the number of zeros, thus preferring axes in the
            # (x,y) plane, etc.
            return 10 * count(n_i -> abs(n_i) > 1e-12, n)
        end
    end
    
    z_dir = select_axis(axes_counts)

    # Collect all symmetry axes orthogonal to the primary axis, along with their
    # counts
    orthogonal_axes_counts = filter(x -> abs(x[1]â‹…z_dir) < 1e-12, axes_counts)

    if isempty(orthogonal_axes_counts)
        println("Warning: Could not find a symmetry axis orthogonal to $z_dir.")
        x_dir = (z_dir â‰ˆ Vec3(1,0,0)) ? Vec3(0,0,1) : Vec3(1,0,0)
        x_dir = normalize(x_dir - (x_dirâ‹…z_dir)*z_dir)
    else
        x_dir = select_axis(orthogonal_axes_counts)
    end

    y_dir = z_dir Ã— x_dir

    # TODO: Very likely should return instead row vectors: [xdir; ydir; zdir].
    # Need to test!
    return Mat3(hcat(x_dir, y_dir, z_dir))
end


"""
    all_symmetry_related_anisotropies(cryst, i_ref, Î›_ref)

Return two lists. The first list contains all atoms `i` that are symmetry
equivalent to `i_ref`. The second list contains the appropriately transformed
anisotropy matrices `Î›` for each site `i`.
"""
function all_symmetry_related_anisotropies(cryst::Crystal, i_ref::Int, Î›_ref)
    @assert is_anisotropy_valid(cryst, i_ref, Î›_ref)

    is = all_symmetry_related_atoms(cryst, i_ref)
    Î›s = map(is) do i
        # Since i is constructed to be symmetry related to i_ref, there must be
        # some symop s that transforms i_ref into i.
        s = first(symmetries_between_atoms(cryst, i, i_ref))
        
        # Rotation+reflection R corresponds to a pure rotation Q that acts on
        # pseudo-vector spins.
        R = cryst.lat_vecs * s.R * inv(cryst.lat_vecs)
        Q = det(R) * R

        # In moving from site i_ref to i, a spin S_ref rotates to S = Q S_ref.
        # Transform the anisotropy operator using the inverse rotation so that
        # the energy remains invariant when applied to the transformed spins.
        return rotate_operator(Î›_ref, Q')
    end

    return (is, Î›s)
end
