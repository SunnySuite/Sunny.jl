# Hamiltonian model parameters and energy/force calculations

function Interactions(nb, latsize, N)
    extfield = zeros(Vec3, latsize..., nb)
    anisos = fill(SingleIonAnisotropies(N), nb)
    pairexch = [PairExchanges() for _ in 1:nb]
    ewald = nothing

    return Interactions(extfield, anisos, pairexch, ewald)
end


"""
    enable_dipole_dipole!(sys::System)

Enables long-range dipole-dipole interactions,

```math
    -(Œº‚ÇÄ/4œÄ) ‚àë_{‚ü®ij‚ü©}  (3 (ùêå_j‚ãÖùê´ÃÇ_{ij})(ùêå_i‚ãÖùê´ÃÇ_{ij}) - ùêå_i‚ãÖùêå_j) / |ùê´_{ij}|^3
```

where the sum is over all pairs of spins (singly counted), including periodic
images, regularized using the Ewald summation convention. The magnetic moments
are ``ùêå_i = Œº_B g ùêí_i`` where ``g`` is the g-factor or g-tensor, and ``ùêí_i``
is the spin angular momentum dipole in units of ƒß. The Bohr magneton ``Œº_B`` and
vacuum permeability ``Œº_0`` are physical constants, with numerical values
determined by the unit system.
"""
function enable_dipole_dipole!(sys::System)
    sys.interactions.ewald = Ewald(sys)
    return
end

"""
    set_external_field!(sys::System, B::Vec3)

Sets the external field `B` that couples to all spins.
"""
function set_external_field!(sys::System, B)
    for idx in all_sites(sys)
        set_external_field_at!(sys, B, idx)
    end
end

"""
    set_external_field_at!(sys::System, B::Vec3, idx::Site)

Sets a Zeeman coupling between a field `B` and a single spin. [`Site`](@ref)
includes a unit cell and a sublattice index.
"""
function set_external_field_at!(sys::System, B, idx)
    idx = Site(idx)
    g = sys.gs[idx[4]]
    sys.interactions.extfield[idx] = sys.units.ŒºB * g' * Vec3(B)
end


"""
    energy(sys::System)

Computes the total system energy.
"""
function energy(sys::System{N}) where N
    (; dipoles, coherents) = sys
    (; extfield, anisos, pairexch, ewald) = sys.interactions

    E = 0.0
    latsize = size(dipoles)[1:3]

    # Zeeman coupling to external field
    for idx in all_sites(sys)
        E -= extfield[idx] ‚ãÖ dipoles[idx]
    end

    # Single-ion anisotropy, dipole or SUN mode
    if N == 0
        for idx in all_sites(sys)
            E_idx, _ = energy_and_gradient_for_classical_anisotropy(dipoles[idx], anisos[idx[4]].clsrep)
            E += E_idx
        end
    else
        for idx in all_sites(sys)
            Œõ = anisos[idx[4]].matrep
            Z = coherents[idx]
            E += real(dot(Z, Œõ, Z))
        end
    end

    for (; heisen, quadmat, biquad) in pairexch
        # Heisenberg exchange
        for (culled, bond, J) in heisen
            culled && break
            for cell in all_cells(sys)
                s·µ¢ = dipoles[cell, bond.i]
                s‚±º = dipoles[offsetc(cell, bond.n, latsize), bond.j]
                E += J * dot(s·µ¢, s‚±º)
            end
        end
        # Quadratic exchange
        for (culled, bond, J) in quadmat
            culled && break
            for cell in all_cells(sys)
                s·µ¢ = dipoles[cell, bond.i]
                s‚±º = dipoles[offsetc(cell, bond.n, latsize), bond.j]
                E += dot(s·µ¢, J, s‚±º)
            end
        end
        # Biquadratic exchange
        for (culled, bond, J) in biquad
            culled && break
            for cell in all_cells(sys)
                s·µ¢ = dipoles[cell, bond.i]
                s‚±º = dipoles[offsetc(cell, bond.n, latsize), bond.j]
                E += J * dot(s·µ¢, s‚±º)^2
            end
        end
    end

    # Long-range dipole-dipole
    if !isnothing(ewald)
        E += energy(dipoles, ewald)
    end
    
    return E
end


function local_energy_change(sys::System{N}, idx, state::SpinState) where N
    (; s, Z) = state
    (; dipoles, coherents) = sys
    (; extfield, anisos, pairexch, ewald) = sys.interactions

    s‚ÇÄ = dipoles[idx]
    Z‚ÇÄ = coherents[idx]
    Œîs = s - s‚ÇÄ
    ŒîE = 0.0

    cell, b = splitidx(idx)
    latsize = size(dipoles)[1:3]

    # Zeeman coupling to external field
    ŒîE -= extfield[idx] ‚ãÖ Œîs

    # Single-ion anisotropy, dipole or SUN mode
    if N == 0
        E_new, _ = energy_and_gradient_for_classical_anisotropy(s, anisos[b].clsrep)
        E_old, _ = energy_and_gradient_for_classical_anisotropy(s‚ÇÄ, anisos[b].clsrep)
        ŒîE += E_new - E_old
    else
        Œõ = anisos[b].matrep
        ŒîE += real(dot(Z, Œõ, Z) - dot(Z‚ÇÄ, Œõ, Z‚ÇÄ))
    end

    (; heisen, quadmat, biquad) = pairexch[b]
    # Heisenberg exchange
    for (_, bond, J) in heisen
        s‚±º = dipoles[offsetc(cell, bond.n, latsize), bond.j]
        ŒîE += J * (Œîs ‚ãÖ s‚±º)
    end
    # Quadratic exchange
    for (_, bond, J) in quadmat
        s‚±º = dipoles[offsetc(cell, bond.n, latsize), bond.j]
        ŒîE += dot(Œîs, J, s‚±º)
    end
    # Biquadratic exchange
    for (_, bond, J) in biquad
        s‚±º = dipoles[offsetc(cell, bond.n, latsize), bond.j]
        ŒîE += J * ((s ‚ãÖ s‚±º)^2 - (s‚ÇÄ ‚ãÖ s‚±º)^2)
    end

    if !isnothing(ewald)
        ŒîE += energy_delta(dipoles, ewald, idx, s)
    end

    return ŒîE
end


# Updates B in-place to hold negative energy gradient, -dE/ds, for each spin.
function set_forces!(B::Array{Vec3, 4}, dipoles::Array{Vec3, 4}, sys::System{N}) where N
    (; extfield, anisos, pairexch, ewald) = sys.interactions
    latsize = sys.latsize

    fill!(B, zero(Vec3))

    # Zeeman coupling
    for idx in all_sites(sys)
        B[idx] += extfield[idx]
    end

    # Single-ion anisotropy only contributes in dipole mode. In SU(N) mode, the
    # anisotropy matrix will be incorporated directly into ‚Ñå.
    if N == 0
        for idx in all_sites(sys)
            _, gradE = energy_and_gradient_for_classical_anisotropy(dipoles[idx], anisos[idx[4]].clsrep)
            B[idx] -= gradE
        end
    end
    
    for (; heisen, quadmat, biquad) in pairexch
        # Heisenberg exchange
        for (culled, bond, J) in heisen
            culled && break
            for cell·µ¢ in all_cells(sys)
                cell‚±º = offsetc(cell·µ¢, bond.n, latsize)
                s·µ¢ = dipoles[cell·µ¢, bond.i]
                s‚±º = dipoles[cell‚±º, bond.j]
                B[cell·µ¢, bond.i] -= J  * s‚±º
                B[cell‚±º, bond.j] -= J' * s·µ¢
            end
        end
        # Quadratic exchange
        for (culled, bond, J) in quadmat
            culled && break
            for cell·µ¢ in all_cells(sys)
                cell‚±º = offsetc(cell·µ¢, bond.n, latsize)
                s·µ¢ = dipoles[cell·µ¢, bond.i]
                s‚±º = dipoles[cell‚±º, bond.j]
                B[cell·µ¢, bond.i] -= J  * s‚±º
                B[cell‚±º, bond.j] -= J' * s·µ¢
            end
        end
        # Biquadratic exchange
        for (culled, bond, J) in biquad
            culled && break
            for cell·µ¢ in all_cells(sys)
                cell‚±º = offsetc(cell·µ¢, bond.n, latsize)
                s·µ¢ = dipoles[cell·µ¢, bond.i]
                s‚±º = dipoles[cell‚±º, bond.j]
                B[cell·µ¢, bond.i] -= 2J  * s‚±º * (s·µ¢‚ãÖs‚±º)
                B[cell‚±º, bond.j] -= 2J' * s·µ¢ * (s·µ¢‚ãÖs‚±º)
            end
        end
    end

    if !isnothing(ewald)
        accum_force!(B, dipoles, ewald)
    end
end

set_forces!(B::Array{Vec3}, sys::System{N}) where N = set_forces!(B, sys.dipoles, sys)

"""
    forces(Array{Vec3}, sys::System)

Returns the effective local field (force) at each site, ``ùêÅ = -‚àÇE/‚àÇùê¨``.
"""
function forces(sys::System{N}) where N
    B = zero(sys.dipoles)
    set_forces!(B, sys)
    return B
end
